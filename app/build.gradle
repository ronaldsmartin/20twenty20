apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

buildscript {
    ext.kotlin_version = '1.0.3'
    repositories {
        jcenter()
        maven { url "https://maven.fabric.io/public" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.+'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.itsronald.twenty2020"
        minSdkVersion    rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "0.2.0"

        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [
            android_backup_api_key: "",
            fabric_api_key: ""
        ]
        buildConfigField "String", "GITHUB_ISSUE_BOT_API_KEY", "\"$GITHUB_ISSUE_BOT_API_KEY\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            // Get API Keys from Gradle properties. These are overridden in CI by environmental
            // variables.
            manifestPlaceholders = [
                android_backup_api_key: ANDROID_BACKUP_API_KEY_DEBUG,
                fabric_api_key: FABRIC_API_KEY
            ]

            minifyEnabled false // Currently breaks build when enabled (Kotlin 1.0.2, Dagger 2.2)
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            // Get API Keys from Gradle properties. These are overridden in CI by environmental
            // variables.
            manifestPlaceholders = [
                android_backup_api_key: ANDROID_BACKUP_API_KEY_RELEASE,
                fabric_api_key: FABRIC_API_KEY
            ]

            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Tools
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$rootProject.leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakCanaryVersion"
    // Silences warnings from LeakCanary v1.4-beta2; see http://stackoverflow.com/a/38255810/4499783
    debugCompile "com.squareup.haha:haha:$rootProject.hahaVersion"

    // Android Support Libs
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-vector-drawable:$rootProject.supportLibraryVersion"
    compile "com.android.support:percent:$rootProject.supportLibraryVersion"

    // Dagger2
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    provided "javax.annotation:jsr250-api:$rootProject.javaAnnotationsVersion"

    // RxJava
    compile "io.reactivex:rxandroid:$rootProject.rxAndroidVersion"
    compile "io.reactivex:rxjava:$rootProject.rxJavaVersion"
    compile "io.reactivex:rxkotlin:$rootProject.rxKotlinVersion"
    compile "com.artemzin.rxjava:proguard-rules:$rootProject.rxJavaProguardRulesVersion"
    compile "com.f2prateek.rx.preferences:rx-preferences:$rootProject.rxPreferencesVersion"

    // Helpers
    compile "com.jakewharton.timber:timber:$rootProject.timberVersion"
    compile "com.karumi:dexter:$rootProject.dexterVersion"

    // Reporting
    compile("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlyticsVersion") {
        transitive = true;
    }
    compile("com.crashlytics.sdk.android:answers:$rootProject.answersVersion") {
        transitive = true;
    }

    // Custom Views
    compile "com.github.amlcurran.showcaseview:library:$rootProject.showcaseViewVersion"
    compile ("com.mikepenz:aboutlibraries:$rootProject.aboutLibsVersion") {
        transitive = true
    }
    compile "com.heinrichreimersoftware:android-issue-reporter:$rootProject.issueReporterVersion"
    compile "me.tankery.lib:circularSeekBar:$rootProject.circularSeekbarVersion"
    compile "com.github.ronaldsmartin:android-number-morphing:$rootProject.numberMorphingVersion"

    // Test
    testCompile "junit:junit:$rootProject.jUnitVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testCompile "org.hamcrest:hamcrest-library:$rootProject.hamcrestVersion"
    // Prevent conflicts between the dependencies required by appcompat and com.android.support.test
    androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support.test:runner:$rootProject.androidTestRunnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.androidTestRunnerVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
}
repositories {
    mavenCentral()
    maven { url "https://maven.fabric.io/public" }
}
